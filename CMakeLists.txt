cmake_minimum_required(VERSION 3.23)

# Setup ERM_PLATFORM
if("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Windows")
	set(ERM_PLATFORM "Windows")
	set(ERM_WINDOWS ON)
elseif("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Darwin")
	set(ERM_PLATFORM "MacOS")
	set(ERM_MAC ON)
else()
	message(FATAL_ERROR "Platform not supported: ${CMAKE_HOST_SYSTEM_NAME}")
endif()

# Common includes
include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/ERMConfig.cmake")
include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/ERMUtils.cmake")

# Verify ERM_TARGET_API
if("${ERM_TARGET_API}" STREQUAL "VULKAN")
	set(ERM_VULKAN ON)
elseif("${ERM_TARGET_API}" STREQUAL "OPEN_GL")
	set(ERM_OPEN_GL ON)
elseif("${ERM_TARGET_API}" STREQUAL "DX12" AND ERM_WINDOWS)
	set(ERM_DX12 ON)
else()
	message(FATAL_ERROR "Invalid target api: ${ERM_TARGET_API}")
endif()
message(STATUS "Using ${ERM_TARGET_API} APIs")

# Project declaration
project(
	"ERM_${ERM_PLATFORM}_${ERM_TARGET_API}"
	VERSION 0.0.1
	DESCRIPTION "C++ Game Engine"
	LANGUAGES CXX
)

# Setup configuration types
set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE)

# Install options
set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/ERM" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/bin/Debug" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/bin/Release" CACHE STRING "" FORCE)

# C++ Options
set(CMAKE_CXX_STANDARD 23 CACHE STRING "" FORCE)
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "" FORCE)
set(CMAKE_CXX_STANDARD_REQUIRED ON CACHE BOOL "" FORCE)
set(CMAKE_CXX_FLAGS "" CACHE STRING "" FORCE)
set(CMAKE_EXE_LINKER_FLAGS "" CACHE STRING "" FORCE)

if(ERM_WINDOWS)
	# Disable exceptions on Windows
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /Zc:preprocessor /D_WINDOWS /D_WIN32 /D_HAS_EXCEPTIONS=0" CACHE STRING "" FORCE)
elseif(ERM_MAC)
	# Enable obj-c++ on MacOS
	set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -x objective-c" CACHE STRING "" FORCE)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -x objective-c++" CACHE STRING "" FORCE)
endif()

# Custom properties
set(CMAKE_EXPORT_COMPILE_COMMANDS ON CACHE STRING "" FORCE)
set(ERM_RES_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/res")
set(ERM_RES_DEST "${CMAKE_INSTALL_PREFIX}/res")

# IDEs friendly
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Setup Python
find_package(Python3 REQUIRED)

# Setup ERM_TARGET_API
erm_setup_api()
erm_setup_pipeline_script()

# Add dependencies based on platform
if(ERM_WINDOWS)
	include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/MSVCConfig.cmake")
elseif(ERM_MAC)
	include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/OSXConfig.cmake")
endif()

# Setup externals
set(ERM_EXTERNALS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/externals")
add_subdirectory("${ERM_EXTERNALS_DIR}")

# Setup libs
set(ERM_LIBS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/libs")
add_subdirectory("${ERM_LIBS_DIR}")

cmake_minimum_required(VERSION 3.21)

# Setup ERM_PLATFORM
if("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Windows")
	set(ERM_PLATFORM "Windows")
elseif("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Darwin")
	set(ERM_PLATFORM "MacOS")
endif()

if("${ERM_PLATFORM}" STREQUAL "Windows")
	set(ERM_WINDOWS ON)
elseif("${ERM_PLATFORM}" STREQUAL "MacOS")
	set(ERM_MAC ON)
else()
	message(FATAL_ERROR "Invalid platform: ${ERM_PLATFORM}")
endif()

# Verify ERM_TARGET_API
if(NOT DEFINED ERM_TARGET_API)
	set(ERM_TARGET_API "VULKAN")
endif()

if("${ERM_TARGET_API}" STREQUAL "VULKAN")
	set(ERM_VULKAN ON)
elseif("${ERM_TARGET_API}" STREQUAL "OPEN_GL")
	set(ERM_OPEN_GL ON)
elseif("${ERM_TARGET_API}" STREQUAL "DX12" AND ERM_WINDOWS)
	set(ERM_DX12 ON)
else()
	message(FATAL_ERROR "Invalid target api: ${ERM_TARGET_API}")
endif()

# Project declaration
project(
	"ERM_${ERM_PLATFORM}_${ERM_TARGET_API}"
	VERSION 0.0.1
	DESCRIPTION "C++ Game Engine"
	LANGUAGES CXX
)

# Setup configuration types
set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE)

# Install options
set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/ERM" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/bin/Debug" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/bin/Release" CACHE STRING "" FORCE)

# C++ Options
set(CMAKE_CXX_STANDARD 23 CACHE STRING "" FORCE)
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "" FORCE)
set(CMAKE_CXX_STANDARD_REQUIRED ON CACHE BOOL "" FORCE)
set(CMAKE_CXX_FLAGS "${GCC_COVERAGE_COMPILE_FLAGS}" CACHE STRING "" FORCE)
set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}" CACHE STRING "" FORCE)

# Disable exceptions on windows
if(ERM_WINDOWS)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /Zc:preprocessor /D_WINDOWS /D_WIN32 /D_HAS_EXCEPTIONS=0" CACHE STRING "" FORCE)
endif()

# Custom properties
set(CMAKE_EXPORT_COMPILE_COMMANDS ON CACHE STRING "" FORCE)
set(ERM_RES_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/res")
set(ERM_RES_DEST "${CMAKE_INSTALL_PREFIX}/res")

# IDEs friendly
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Setup Python
find_package(Python3)
if(NOT DEFINED Python3_FOUND OR NOT ${Python3_FOUND})
	message(FATAL_ERROR "Python3 not found")
endif()

# Setup CMAKE_MODULE_PATH
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(ERMConfig)
include(ERMUtils)

# Setup ERM_TARGET_API
erm_setup_api()
erm_setup_pipeline_script()
message(STATUS "Using ${ERM_TARGET_API} APIs")

# Add dependencies based on ERM_TARGET_API
if(ERM_OPEN_GL)
	include(GLEWConfig)
	add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glew-cmake")
endif()

# Add dependencies based on platform
if(ERM_WINDOWS)
	include(MSVCConfig)
elseif(ERM_MAC)
	include(OSXConfig)
endif()

# Setup externals
set(ERM_EXTERNALS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/externals")
add_subdirectory("${ERM_EXTERNALS_DIR}")

# Setup libs
set(ERM_LIBS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/libs")
add_subdirectory("${ERM_LIBS_DIR}")

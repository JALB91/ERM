# CMAKE VERSION
cmake_minimum_required(VERSION 3.15...3.17)

# PROJECT DECLARATION
project(
	"ERM_${TARGET_API}"
	VERSION 0.0.1
	DESCRIPTION "C++ Game Engine"
	LANGUAGES CXX
)

# BUILD TYPE
if(NOT DEFINED CMAKE_BUILD_TYPE OR (NOT "${CMAKE_BUILD_TYPE}" STREQUAL "Debug" AND NOT "${CMAKE_BUILD_TYPE}" STREQUAL "Release"))
	set(CMAKE_BUILD_TYPE "Debug")
endif()

set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE)

# INSTALL OPTIONS
set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/${PROJECT_NAME}")
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib/")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib/")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/bin/")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}")

file(MAKE_DIRECTORY "${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}${CMAKE_BUILD_TYPE}")
file(MAKE_DIRECTORY "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}${CMAKE_BUILD_TYPE}")
file(MAKE_DIRECTORY "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}")

# C++ OPTIONS
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")

# CUSTOM PROPERTIES
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(RES_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/res")
set(RES_DEST "${CMAKE_INSTALL_PREFIX}/res")

# IDEs FRIENDLY
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# SETUP CMAKE_MODULE_PATH
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(macros)
include(ERMConfig)
include(AssimpConfig)

# SETUP TARGET API
setup_api()
message(STATUS "Using ${TARGET_API} APIs")
message(STATUS "${CMAKE_BUILD_TYPE} Build")

# ADD DEPENDENCIES BASED ON TARGET API
if("${TARGET_API}" STREQUAL "OpenGL")
	include(GLEWConfig)
	add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glew-cmake")
	set(GLEW_LIBRARY libglew_static)
	set(PUBLIC_DEPENDENCIES "${GLEW_LIBRARY}")
endif("${TARGET_API}" STREQUAL "OpenGL")

# INCLUDE CONFIGURATION FILES FOR DEPENDENCIES
include(TracyConfig)
include(GLMConfig)
include(GLFWConfig)
include(TinyXmlConfig)
include(FindFbx)

if(NOT ${ERM_ASSIMP_ENABLED})
	find_package(Fbx)
endif(NOT ${ERM_ASSIMP_ENABLED})

if(NOT ${FBX_FOUND})
	message(WARNING "Cannot find FBX libraries")
else()
	set(ERM_FBX_ENABLED TRUE)
	list(APPEND PRIVATE_DEBUG_DEPENDENCIES "${FBX_LIBRARIES_DEBUG}")
	list(APPEND PRIVATE_RELEASE_DEPENDENCIES "${FBX_LIBRARIES}")
endif(NOT ${FBX_FOUND})

# ADDS COMMON EXTERNALS DEPENDENCIES
add_subdirectory ("${CMAKE_CURRENT_SOURCE_DIR}/externals/tracy")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glm")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glfw")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/imgui")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/tinyxml")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/stb_image")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/SPIRV-Cross")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/fmod")

if(ERM_ASSIMP_ENABLED)
	add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/assimp")
endif(ERM_ASSIMP_ENABLED)

# GATHER SOURCES
file(GLOB_RECURSE SOURCES 
	"${CMAKE_CURRENT_SOURCE_DIR}/Sources/common/*.h"
	"${CMAKE_CURRENT_SOURCE_DIR}/Sources/${TARGET_API}/*.h"
	"${CMAKE_CURRENT_SOURCE_DIR}/Sources/common/*.cpp"
	"${CMAKE_CURRENT_SOURCE_DIR}/Sources/${TARGET_API}/*.cpp"
	"${CMAKE_CURRENT_SOURCE_DIR}/res/shaders/${TARGET_API}/*"
)
if(NOT ERM_RAY_TRACING_ENABLED)
	list(FILTER SOURCES EXCLUDE REGEX ".*/ray_tracing/.*")
endif(NOT ERM_RAY_TRACING_ENABLED)
create_groups("${SOURCES}")

# CREATE EXECUTABLE IF STANDALONE, LIBRARY OTHERWISE
if("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_SOURCE_DIR}")
	add_executable("${PROJECT_NAME}" "${SOURCES}")
else()
	add_library("${PROJECT_NAME}" "${SOURCES}")
endif("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_SOURCE_DIR}")

# SETUP PLATFORM SPECIFIC OPTIONS
if(MSVC OR WIN)
	include(MSVCConfig)
elseif(XCODE OR APPLE)
	include(OSXConfig)
else()
	message(FATAL_ERROR "Unsupported Platform")
endif()
target_setup_project("${PROJECT_NAME}")

# SETUP SHADER COMPILER ARGS
if(SHADERS_COMPILER)
	if(ERM_RAY_TRACING_ENABLED)
		set(SHADERS_COMPILER "${SHADERS_COMPILER} --target-spv=spv1.5 -I ${RES_DEST}/shaders/Vulkan/imports/")
	else()
		set(SHADERS_COMPILER "${SHADERS_COMPILER} --target-spv=spv1.3 -I ${RES_DEST}/shaders/Vulkan/imports/")
	endif(ERM_RAY_TRACING_ENABLED)
endif(SHADERS_COMPILER)

# TARGET INCLUDE DIRECTORIES
target_include_directories(
	"${PROJECT_NAME}"
	PRIVATE
		"${TARGET_API_INCLUDE_DIR}"
		"${PRIVATE_INCLUDES}"
		"${CMAKE_CURRENT_SOURCE_DIR}/Sources/common/"
		"${CMAKE_CURRENT_SOURCE_DIR}/Sources/${TARGET_API}/"
		$<$<BOOL:${ERM_FBX_ENABLED}>:${FBX_INCLUDE_DIRS}>
		$<$<BOOL:${ERM_ASSIMP_ENABLED}>:${ASSIMP_INCLUDE_DIR}>
	PUBLIC 
		"${PUBLIC_INCLUDES}"
)

# TARGET COMMON CONFIGURATIONS
target_compile_definitions(
	"${PROJECT_NAME}"
	PUBLIC
		$<$<STREQUAL:"${TARGET_API}","Vulkan">:GLM_FORCE_DEPTH_ZERO_TO_ONE>
		$<$<STREQUAL:"${TARGET_API}","Vulkan">:IMGUI_DISABLE_OBSOLETE_FUNCTIONS>
		$<$<BOOL:${ERM_FLIP_PROJECTION}>:ERM_FLIP_PROJECTION>
		$<$<BOOL:${ERM_FLIP_VIEWPORT}>:ERM_FLIP_VIEWPORT>
		$<$<BOOL:${ERM_FBX_ENABLED}>:ERM_FBX_ENABLED>
		$<$<BOOL:${ERM_ASSIMP_ENABLED}>:ERM_ASSIMP_ENABLED>
		$<$<BOOL:${ERM_RAY_TRACING_ENABLED}>:ERM_RAY_TRACING_ENABLED>
		${TARGET_API_COMPILE_DEF}
	PRIVATE
		$<$<STREQUAL:"${TARGET_API}","OpenGL">:GLEW_STATIC>
		$<$<STREQUAL:"${TARGET_API}","Vulkan">:ERM_SHADER_COMPILER="${SHADERS_COMPILER}">
)

# TARGET COMMON FEATURES
target_compile_features(
	"${PROJECT_NAME}"
	PUBLIC 
		cxx_std_17
)

# TARGET COMMON PROPERTIES
set_target_properties(
	"${PROJECT_NAME}" 
	PROPERTIES
		CXX_STANDARD 17
		CXX_EXTENSIONS OFF
)

# LINK LIBRARIES
target_link_libraries(
	"${PROJECT_NAME}"
	PRIVATE 
		"${TARGET_API_LIB}"
		glfw
		$<$<BOOL:${ERM_ASSIMP_ENABLED}>:assimp>
		stb_image
		spirv-cross-cpp
		fmod_core
		fmod_studio
		debug "${PRIVATE_DEBUG_DEPENDENCIES}"
		optimized "${PRIVATE_RELEASE_DEPENDENCIES}"
	PUBLIC 
		"${PUBLIC_DEPENDENCIES}"
		glm
		imgui
		tinyxml2
		Tracy::TracyClient
)

# CUSTOM TARGETS
set(PIPELINE_ARGS --res-dest ${RES_DEST} --res-src ${RES_SOURCE})

if("${TARGET_API}" STREQUAL "Vulkan")
	list(APPEND PIPELINE_ARGS --shaders-compiler ${SHADERS_COMPILER})
endif("${TARGET_API}" STREQUAL "Vulkan")

if(${ERM_RAY_TRACING_ENABLED})
	list(APPEND PIPELINE_ARGS --rtx-enabled True)
endif(${ERM_RAY_TRACING_ENABLED})

set(PIPELINE_SCRIPT "${CMAKE_CURRENT_SOURCE_DIR}/scripts/resources_pipeline.py")

add_custom_target(
	PIPELINE
	COMMAND ${PYTHON_EXECUTABLE} ${PIPELINE_SCRIPT} ${PIPELINE_ARGS}
	COMMENT "Resources Pipeline"
)
add_dependencies("${PROJECT_NAME}" PIPELINE)

# DEBUG PRINT VARIABLES
if("${PRINT_VARIABLES}")
	get_cmake_property(VARIABLE_NAMES VARIABLES)
	list (SORT VARIABLE_NAMES)
	foreach(VARIABLE_NAME ${VARIABLE_NAMES})
    	message(STATUS "${VARIABLE_NAME}=${${VARIABLE_NAME}}")
	endforeach(VARIABLE_NAME ${VARIABLE_NAMES})

	execute_process(COMMAND "${CMAKE_COMMAND}" "-E" "environment")
endif("${PRINT_VARIABLES}")

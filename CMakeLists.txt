# CMAKE VERSION
cmake_minimum_required(VERSION 3.15...3.17)

# PROJECT DECLARATION
project(
	"ERM"
	VERSION 0.0.1
	DESCRIPTION "C++ Game Engine"
	LANGUAGES CXX
)

# INSTALL OPTIONS
set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/${CMAKE_BUILD_TYPE}")
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/bin")

# BUILD TYPE
if(NOT DEFINED CMAKE_BUILD_TYPE OR (NOT "${CMAKE_BUILD_TYPE}" STREQUAL "Debug" AND NOT "${CMAKE_BUILD_TYPE}" STREQUAL "Release"))
	set(CMAKE_BUILD_TYPE "Debug")
endif()
message(STATUS "Configuring ${CMAKE_BUILD_TYPE} build")

# ENSURE ONLY ACTIVE BUILD TYPE IS ENABLED
set(CMAKE_CONFIGURATION_TYPES "${CMAKE_BUILD_TYPE}" CACHE STRING "" FORCE)

# C++ OPTIONS
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")


# IDEs FRIENDLY
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# SETUP CMAKE_MODULE_PATH
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(macros)
include(ERMConfig)

# SETUP TARGET API
setup_api()
message(STATUS "Using ${TARGET_API} APIs")

# ADD DEPENDENCIES BASED ON TARGET API
if("${TARGET_API}" STREQUAL "OpenGl")
	include(GLEWConfig)
	add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glew-cmake")
	set(GLEW_LIBRARY libglew_static)
	set(PUBLIC_DEPENDENCIES "${GLEW_LIBRARY}")
elseif("${TARGET_API}" STREQUAL "Vulkan")
	add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/Vulkan-Hpp")
	set(VULKAN_HPP_INSTALL TRUE)
endif()

# INCLUDE CONFIGURATION FILES FOR DEPENDENCIES
include(GLMConfig)
include(GLFWConfig)
include(TinyXmlConfig)

# ADDS COMMON EXTERNALS DEPENDENCIES
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glm")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/glfw")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/imgui")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/tinyxml")
add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/externals/stb_image")

# GATHER SOURCES
file(GLOB_RECURSE SOURCES 
	"${CMAKE_CURRENT_SOURCE_DIR}/include/common/*.h"
	"${CMAKE_CURRENT_SOURCE_DIR}/include/${TARGET_API}/*.h"
	"${CMAKE_CURRENT_SOURCE_DIR}/src/common/*.cpp"
	"${CMAKE_CURRENT_SOURCE_DIR}/src/${TARGET_API}/*.cpp"
)
create_groups("${SOURCES}")

# CREATE EXECUTABLE IF STANDALONE, LIBRARY OTHERWISE
if("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_SOURCE_DIR}")
	add_executable("${PROJECT_NAME}" "${SOURCES}")
else()
	add_library("${PROJECT_NAME}" "${SOURCES}")
endif("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_SOURCE_DIR}")

# LINK LIBRARIES
target_link_libraries(
	"${PROJECT_NAME}"
	PRIVATE 
		"${TARGET_API_LIB}"
		"${PRIVATE_DEPENDENCIES}"
		glfw
		stb_image
	PUBLIC 
		"${PUBLIC_DEPENDENCIES}"
		glm
		imgui
		tinyxml2
)

# TARGET INCLUDE DIRECTORIES
target_include_directories(
	"${PROJECT_NAME}"
	PRIVATE
		"${TARGET_API_INCLUDE_DIR}"
		"${PRIVATE_INCLUDES}"
		"${CMAKE_CURRENT_SOURCE_DIR}/include/common/"
		"${CMAKE_CURRENT_SOURCE_DIR}/include/${TARGET_API}/"
	PUBLIC 
		"${PUBLIC_INCLUDES}"
)

# TARGET COMMON CONFIGURATIONS
target_compile_definitions(
	"${PROJECT_NAME}"
	PUBLIC
		$<$<STREQUAL:"${TARGET_API}","Vulkan">:GLM_FORCE_DEPTH_ZERO_TO_ONE>
	PRIVATE
		$<$<STREQUAL:"${TARGET_API}","OpenGl">:GLEW_STATIC>
)

# TARGET COMMON FEATURES
target_compile_features(
	"${PROJECT_NAME}"
	PUBLIC 
		cxx_std_17
)

# TARGET COMMON PROPERTIES
set_target_properties(
	"${PROJECT_NAME}" 
	PROPERTIES
		CXX_STANDARD 17
		CXX_EXTENSIONS OFF
		ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib"
		LIBRARY_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib"
		RUNTIME_OUTPUT_DIRECTORY "${CMAKE_INSTALL_PREFIX}/bin"
)

# SETUP PLATFORM SPECIFIC OPTIONS
if(MSVC OR WIN)
	include(MSVCConfig)
elseif(XCODE OR APPLE)
	include(OSXConfig)
else()
	message(FATAL_ERROR "Unsupported Platform")
endif()
target_setup_project("${PROJECT_NAME}")

# CUSTOM PROPERTIES
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(RES_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/res/")
set(RES_DEST "${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_BUILD_TYPE}/res/")

# CUSTOM COMMANDS
add_custom_command(
	TARGET "${PROJECT_NAME}" PRE_BUILD
 	COMMAND "${CMAKE_COMMAND}" -E copy_directory "${RES_SOURCE}" "${RES_DEST}"
 	DEPENDS "${RES_DEST}"
 	COMMENT "Copy resources folder from ${RES_SOURCE} to ${RES_DEST}"
)

if("${TARGET_API}" STREQUAL "Vulkan")
	file(GLOB SHADERS
		"${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_BUILD_TYPE}/res/shaders/vk_*"
	)

	foreach(SHADER ${SHADERS})
		add_custom_command(
			TARGET "${PROJECT_NAME}" PRE_BUILD
			COMMAND "${SHADERS_COMPILER}" "${SHADER}" -o "${SHADER}"
			COMMENT "Compiling shader ${SHADER}"
		)
	endforeach(SHADER)
endif("${TARGET_API}" STREQUAL "Vulkan")

# DEBUG PRINT VARIABLES
if("${PRINT_VARIABLES}")
	get_cmake_property(VARIABLE_NAMES VARIABLES)
	list (SORT VARIABLE_NAMES)
	foreach(VARIABLE_NAME ${VARIABLE_NAMES})
    	message(STATUS "${VARIABLE_NAME}=${${VARIABLE_NAME}}")
	endforeach(VARIABLE_NAME ${VARIABLE_NAMES})

	execute_process(COMMAND "${CMAKE_COMMAND}" "-E" "environment")
endif("${PRINT_VARIABLES}")

cmake_minimum_required(VERSION 3.30)

# Setup ERM_HOST_PLATFORM
if("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Windows")
	set(ERM_HOST_PLATFORM "WIN")
	set(ERM_WINDOWS ON)
elseif("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Darwin")
	set(ERM_HOST_PLATFORM "OSX")
	set(ERM_OSX ON)
else()
	message(FATAL_ERROR "Platform not supported: ${CMAKE_HOST_SYSTEM_NAME}")
endif()

# Setup ERM_CMAKE_GENERATOR
if("${CMAKE_GENERATOR}" STREQUAL "Xcode")
	set(ERM_CMAKE_GENERATOR "XCODE")
	set(ERM_GENERATOR_XCODE ON)
elseif("${CMAKE_GENERATOR}" STREQUAL "Ninja")
	set(ERM_CMAKE_GENERATOR "NINJA")
	set(ERM_GENERATOR_NINJA ON)
elseif("${CMAKE_GENERATOR}" STREQUAL "Visual Studio 17 2022")
	set(ERM_CMAKE_GENERATOR "VS")
	set(ERM_GENERATOR_VS ON)
elseif("${CMAKE_GENERATOR}" STREQUAL "Unix Makefiles")
	set(ERM_CMAKE_GENERATOR "UNIXMK")
	set(ERM_GENERATOR_UNIXMK ON)
else()
	message(FATAL_ERROR "CMake Generator not supported: ${CMAKE_GENERATOR}")
endif()

# Common includes
include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/ERMConfig.cmake")
include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/ERMUtils.cmake")

# Verify ERM_RENDERING_API
if("${ERM_RENDERING_API}" STREQUAL "VULKAN")
	set(ERM_VULKAN ON)
elseif("${ERM_RENDERING_API}" STREQUAL "OPEN_GL")
	set(ERM_OPEN_GL ON)
elseif("${ERM_RENDERING_API}" STREQUAL "DX12" AND ERM_WINDOWS)
	set(ERM_DX12 ON)
else()
	message(FATAL_ERROR "Invalid rendering api: ${ERM_RENDERING_API}")
endif()
message(STATUS "Using ${ERM_RENDERING_API} APIs")

# Project declaration
project(
	"ERM_${ERM_HOST_PLATFORM}_${ERM_CMAKE_GENERATOR}_${ERM_RENDERING_API}"
	VERSION 0.0.1
	DESCRIPTION "C++ Game Engine"
	LANGUAGES CXX
)

# Setup configuration types
set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE)

# Install options
set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/ERM" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/lib/Debug" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_INSTALL_PREFIX}/bin/Debug" CACHE STRING "" FORCE)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/lib/Release" CACHE STRING "" FORCE)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_INSTALL_PREFIX}/bin/Release" CACHE STRING "" FORCE)

# C++ Options
set(CMAKE_CXX_STANDARD 23 CACHE STRING "" FORCE)
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "" FORCE)
set(CMAKE_CXX_STANDARD_REQUIRED ON CACHE BOOL "" FORCE)
set(CMAKE_CXX_FLAGS "" CACHE STRING "" FORCE)
set(CMAKE_EXE_LINKER_FLAGS "" CACHE STRING "" FORCE)
set(CMAKE_CXX_SCAN_FOR_MODULES OFF CACHE BOOL "" FORCE)

if(ERM_WINDOWS)
	# Disable exceptions on WIN
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /Zc:preprocessor /D_WINDOWS /D_WIN32 /D_HAS_EXCEPTIONS=0" CACHE STRING "" FORCE)
elseif(ERM_OSX AND ERM_GENERATOR_XCODE)
	# Enable obj-c on OSX
	set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -x objective-c" CACHE STRING "" FORCE)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -x objective-c++" CACHE STRING "" FORCE)
endif()

if(ERM_GENERATOR_NINJA OR ERM_GENERATOR_UNIXMK)
	if(NOT CMAKE_BUILD_TYPE)
		set(CMAKE_BUILD_TYPE "Release" CACHE STRING "" FORCE)
	endif()
endif()

# Custom properties
set(CMAKE_EXPORT_COMPILE_COMMANDS ON CACHE STRING "" FORCE)
set(ERM_RES_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/res")
set(ERM_RES_DEST "${CMAKE_INSTALL_PREFIX}/res")

# IDEs friendly
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Setup Python
find_package(Python3 REQUIRED)

# Setup ERM_RENDERING_API
erm_setup_api()
erm_setup_pipeline_script()

# Add dependencies based on platform
if(ERM_WINDOWS)
	include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/MSVCConfig.cmake")
elseif(ERM_OSX)
	include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/OSXConfig.cmake")
endif()

# Setup externals
set(ERM_EXTERNALS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/externals")
add_subdirectory("${ERM_EXTERNALS_DIR}")

# Setup libs
set(ERM_LIBS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/libs")
add_subdirectory("${ERM_LIBS_DIR}")
